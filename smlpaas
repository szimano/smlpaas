#!/usr/bin/env groovy
import static Constants.*

if (args) {
	switch(args[0]) {
		case "deploy":
			deployCMD(args.drop(1))
			break
		case "setup":
			setupCMD(args.drop(1))
			break
		case "restart":
			restartCMD(args.drop(1))
			break
		case "tail":
			tailCMD(args.drop(1))
			break
		default:
			println "Unknown command: '${args[0]}'"
			break
	}
}
else {
	println "usage\n smlpaas [command]"
}

class Constants {
    static final CONFIG_FILE_NAME = ".smlpaas"
	static final PROFILES = ["tomcat7", "jar", "wildfly8", "jboss7"]
}

def deployCMD(String[] args) {	
	if (args.length != 1) {
		println "usage\n smlpaas deploy WAR_FILE"
	} else {
		String pathToWar = getFullPath(args[0])
		
		String fileName = new File(pathToWar).name
		
		Config config = figureServer(pathToWar)
		String serverURL = config.url
		
		if (new File(pathToWar).exists()) {
			println "Stoping server"
			def stopCMD = "ssh app@$serverURL \"scripts/${config.profile}/stop.sh\""
			runAndWaitCMD(stopCMD)
			
			println "Uploading $pathToWar"
			def copyCMD =  "rsync --partial --progress --rsh=ssh $pathToWar app@$serverURL:dropbox/ROOT.war"
			runAndWaitCMD(copyCMD)
			
			println "Deploying WAR"
			def deployCMD = "ssh app@$serverURL \"scripts/${config.profile}/deploy.sh\""
			runAndWaitCMD(deployCMD)
			
			println "Starting server"
			def startCMD = "ssh app@$serverURL scripts/${config.profile}/start.sh \"$config.runParams\""
			println startCMD
			runAndWaitCMD(startCMD)
			
			println "$pathToWar deployed and server started"
		} else {
			println "The WAR file  ($pathToWar) does not exist"
		}
	}
}

def getFullPath(String path) {
	if (!path.startsWith("/")) {
		return new File(path).canonicalPath
	}
	else {
		return path
	}
}

def figureServer(String path) {
	File file = new File(path)
	while (file != null) {
		if (file.isDirectory()) {
			File config = new File(CONFIG_FILE_NAME, file)
			if (config.exists()) {
				def props = new Properties()
				config.withInputStream { 
  			  		stream -> props.load(stream) 
				}
				
				def configuration = new Config(url: props["url"], profile: props["profile"], runParams: props["runParams"])
				println "Using config: $configuration from $config.name"
				
				return configuration
			}
		}
		file = file.parentFile
	}
	
	System.err.println "Could not figure out server config for file $path. Maybe do smlpaas config first?"
	System.exit(-1)
}

def setupCMD(String[] args) {	
	if (args.length < 2 || args.length > 4) {
		println "usage\n smlpaas setup DIRECTORY SERVER_IP [PROFILE] [RUN_PARAMS]"
	} else {
		def dirName = args[0]
		def ip = args[1]
		def profile = args.length >= 3 ? args[2] : "tomcat7"
		def runParams = args.length >= 4 ? args[3] : ""
		
		if (!(profile in PROFILES)) {
			println "Unknown profile: $profile, available profiles: $PROFILES"
		}
		
		File dir = new File(dirName)
		if (dir.exists() && dir.isDirectory()) {
			File configFile = new File(CONFIG_FILE_NAME, dir)
			configFile.delete()
			configFile << "url: $ip\n"
			configFile << "profile: $profile\n"
			configFile << "runParams: $runParams\n"
			
			println "Setup for server $ip at $dirName completed"
		} else {
			println "ERROR $dirName does not exist or is not a directory"
		}
	}
}

def restartCMD(String[] args) {
	def config
	if (args.length == 0) {
		config = figureServer(".")
	} else {
		config = figureServer(args[0])
	}
	String serverURL = config.url
	
	restart(serverURL)
}


def tailCMD(String[] args) {
	def config
	if (args.length == 0) {
		config = figureServer(".")
	} else {
		config = figureServer(args[0])
	}
	String serverURL = config.url
	String tailCMD = "ssh app@$serverURL \"scripts/${config.profile}/tail.sh\""
	runCMD(tailCMD)
		
	pause()
}

def restart(String serverURL) {
	String restartCMD = "ssh -n -f app@$serverURL \"scripts/${config.profile}/restart.sh\""
	runAndWaitCMD(restartCMD)
}

def runCMD(String cmd) {
	def process = cmd.execute()
	process.consumeProcessOutput(System.out, System.err)
	return process
}

def runAndWaitCMD(String cmd) {
	def process = runCMD(cmd)
	process.waitFor()
}

def pause() {
	System.in.withReader {
		println it.readLine()
	}
}

@groovy.transform.Canonical
class Config {
def url
def profile
def runParams
}